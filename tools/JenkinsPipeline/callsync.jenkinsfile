pipeline {
    agent {
        label 'built-in'
    }

    environment {
        API_BASE = 'http://192.168.110.112:22333/api/PowerShell'
    }

    stages {
        stage('Submit Script') {
            steps {
                script {
                    def response = httpRequest(
                        url: "${API_BASE}/run",
                        httpMode: 'POST',
                        contentType: 'APPLICATION_JSON',
                        requestBody: '{"scriptPath":"C:\\\\tools\\\\gitsync\\\\sync\\\\sync.ps1"}',
                        validResponseCodes: '200'
                    )
                    def json = readJSON text: response.content
                    if (json.code != 200) {
                        error "Submit failed: ${json.message}"
                    }
                    env.TASK_ID = json.data
                    echo "Task submitted, ID: ${env.TASK_ID}"
                }
            }
        }

        stage('Wait for Completion') {
            steps {
                script {
                    def interval = 10
                    def lastOutputLen = 0
                    def lastErrorLen = 0
                    while (true) {
                        sleep interval
                        def resp = httpRequest(
                            url: "${API_BASE}/${env.TASK_ID}",
                            httpMode: 'GET',
                            validResponseCodes: '200'
                        )
                        def result = readJSON text: resp.content
                        def status = result.data?.status
                        def output = result.data?.output ?: ''
                        def errMsg = result.data?.error ?: ''

                        // Print incremental output
                        if (output.length() > lastOutputLen) {
                            echo output.substring(lastOutputLen)
                            lastOutputLen = output.length()
                        }
                        if (errMsg.length() > lastErrorLen) {
                            echo "[STDERR] ${errMsg.substring(lastErrorLen)}"
                            lastErrorLen = errMsg.length()
                        }

                        if (status == 'Completed') {
                            return
                        } else if (status == 'Failed' || status == 'TimedOut' || status == 'Cancelled') {
                            error "Task ${status}"
                        }
                    }
                }
            }
        }
    }
}
