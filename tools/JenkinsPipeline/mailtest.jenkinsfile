pipeline {
    agent {
        label 'built-in'
    }

    environment {
        API_BASE = 'http://192.168.110.112:22333/api/PowerShell'
    }

    stages {
        stage('Submit Script') {
            steps {
                script {
                    def response = httpRequest(
                        url: "${API_BASE}/run",
                        httpMode: 'POST',
                        contentType: 'APPLICATION_JSON',
                        requestBody: '{"scriptPath":"C:\\\\tools\\\\test\\\\mailtest.ps1"}',
                        validResponseCodes: '200'
                    )
                    def json = readJSON text: response.content
                    if (json.code != 200) {
                        error "Submit failed: ${json.message}"
                    }
                    env.TASK_ID = json.data
                    echo "Task submitted, ID: ${env.TASK_ID}"
                }
            }
        }

        stage('Wait for Completion') {
            steps {
                script {
                    env.ALL_ERRORS = ''
                    def interval = 10
                    def outputOffset = 0
                    def errorOffset = 0
                    while (true) {
                        sleep interval
                        def resp = httpRequest(
                            url: "${API_BASE}/${env.TASK_ID}/output?outputOffset=${outputOffset}&errorOffset=${errorOffset}",
                            httpMode: 'GET',
                            validResponseCodes: '200'
                        )
                        def result = readJSON text: resp.content
                        def data = result.data
                        def output = data?.output ?: ''
                        def errMsg = data?.error ?: ''

                        if (output) {
                            echo output
                        }
                        if (errMsg) {
                            echo "[STDERR] ${errMsg}"
                            env.ALL_ERRORS += errMsg + '\n'
                        }

                        outputOffset = data?.outputOffset ?: outputOffset
                        errorOffset = data?.errorOffset ?: errorOffset

                        def status = data?.status
                        if (status == 'Completed') {
                            return
                        } else if (status == 'Failed' || status == 'TimedOut' || status == 'Cancelled') {
                            error "Task ${status}"
                        }
                    }
                }
            }
        }
    }

    post {
        success {
            script {
                def taskStatus = ''
                def exitCode = ''
                if (env.TASK_ID) {
                    try {
                        def resp = httpRequest(
                            url: "${API_BASE}/${env.TASK_ID}",
                            httpMode: 'GET',
                            validResponseCodes: '200'
                        )
                        def result = readJSON text: resp.content
                        taskStatus = result.data?.status ?: ''
                        exitCode = result.data?.exitCode?.toString() ?: ''
                    } catch (Exception e) {
                        echo "Failed to get task status: ${e.message}"
                    }
                }

                build job: 'Notify Email',
                      wait: false,
                      parameters: [
                          string(name: 'NOTIFY_SUBJECT', value: "Jenkins Build Succeeded: ${env.JOB_NAME} #${env.BUILD_NUMBER}"),
                          string(name: 'NOTIFY_JOB_NAME', value: env.JOB_NAME),
                          string(name: 'NOTIFY_BUILD_NUMBER', value: env.BUILD_NUMBER),
                          string(name: 'NOTIFY_BUILD_RESULT', value: currentBuild.currentResult),
                          string(name: 'NOTIFY_TASK_STATUS', value: taskStatus ?: '未知'),
                          string(name: 'NOTIFY_EXIT_CODE', value: exitCode ?: '未知'),
                          string(name: 'NOTIFY_ERRORS', value: ''),
                          string(name: 'NOTIFY_LOG_URL', value: "${env.BUILD_URL}console"),
                          string(name: 'NOTIFY_RECIPIENTS', value: 'yuxuan.tian@hawksoft3d.com')
                      ]
            }
        }
        failure {
            script {
                // 获取任务最终状态
                def taskStatus = ''
                def exitCode = ''
                if (env.TASK_ID) {
                    try {
                        def resp = httpRequest(
                            url: "${API_BASE}/${env.TASK_ID}",
                            httpMode: 'GET',
                            validResponseCodes: '200'
                        )
                        def result = readJSON text: resp.content
                        taskStatus = result.data?.status ?: ''
                        exitCode = result.data?.exitCode?.toString() ?: ''
                    } catch (Exception e) {
                        echo "Failed to get task status: ${e.message}"
                    }
                }

                // 触发邮件通知流水线
                build job: 'Notify Email',
                      wait: false,
                      parameters: [
                          string(name: 'NOTIFY_SUBJECT', value: "Jenkins Build Failed: ${env.JOB_NAME} #${env.BUILD_NUMBER}"),
                          string(name: 'NOTIFY_JOB_NAME', value: env.JOB_NAME),
                          string(name: 'NOTIFY_BUILD_NUMBER', value: env.BUILD_NUMBER),
                          string(name: 'NOTIFY_BUILD_RESULT', value: currentBuild.currentResult),
                          string(name: 'NOTIFY_TASK_STATUS', value: taskStatus ?: '未知'),
                          string(name: 'NOTIFY_EXIT_CODE', value: exitCode ?: '未知'),
                          string(name: 'NOTIFY_ERRORS', value: env.ALL_ERRORS ?: ''),
                          string(name: 'NOTIFY_LOG_URL', value: "${env.BUILD_URL}console"),
                          string(name: 'NOTIFY_RECIPIENTS', value: 'yuxuan.tian@hawksoft3d.com')
                      ]
            }
        }
    }
}
